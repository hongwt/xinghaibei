## Warrior: Protection
## https://www.wowhead.com/wotlk/guide/classes/warrior/protection/tank-rotation-cooldowns-abilities-pve
## November 2023

## Precombat
# 警戒
actions.precombat+=/vigilance,if=group&active_dot.vigilance=0 
# 血性狂暴
actions.precombat+=/bloodrage,use_off_gcd=1,if=rage.current<action.commanding_shout.spend&buff.shout.remains<=60
# 命令怒吼
actions.precombat+=/commanding_shout,if=assigned_shout.commanding_shout&buff.shout.remains<=60
# 战斗怒吼
actions.precombat+=/battle_shout,if=assigned_shout.battle_shout&buff.shout.remains<=60
# 狂暴姿态
actions.precombat+=/berserker_stance,use_off_gcd=1,if=cooldown.recklessness.up
# 鲁莽
actions.precombat+=/recklessness
# 防御姿态
actions.precombat+=/defensive_stance,use_off_gcd=1,if=buff.defensive_stance.down&!cooldown.recklessness.up
actions.precombat+=/bloodrage,use_off_gcd=1,if=rage.deficit>30&!set_bonus.tier10prot_4pc

## Default
actions+=/call_action_list,name=init
# 盾击
actions+=/shield_bash
# 血性狂暴
actions+=/bloodrage,use_off_gcd=1,if=rage.deficit>30&!set_bonus.tier10prot_4pc
actions+=/use_items
actions+=/run_action_list,name=aoe,if=active_enemies>1
actions+=/run_action_list,name=single

## Init Vars
actions.init+=/variable,name=time_to_die,value=(debuff.training_dummy.up&300)|target.time_to_die

## Single
# 碎裂投掷？
actions.single+=/shattering_throw,if=buff.bloodlust.up&debuff.shattering_throw.down
actions.single+=/defensive_stance,use_off_gcd=1,if=buff.defensive_stance.down
# 英勇打击
actions.single+=/heroic_strike,use_off_gcd=1,if=rage.current>=settings.queueing_threshold&rage.current>action.heroic_strike.spend+((cooldown.shield_slam.remains<2)*action.shield_slam.spend)
# 盾牌格挡
actions.single+=/shield_block,if=(aggro|debuff.training_dummy.up)&cooldown.shield_slam.up&rage.current>=action.shield_slam.spend
# 盾牌猛击
actions.single+=/shield_slam
# 复仇
actions.single+=/revenge
actions.single+=/commanding_shout,if=assigned_shout.commanding_shout&buff.shout.remains<=4&buff.commanding_shout.remains<=4
actions.single+=/battle_shout,if=assigned_shout.battle_shout&buff.shout.remains<=4&buff.battle_shout.remains<=4
# 挫志怒吼
actions.single+=/demoralizing_shout,if=settings.debuff_demoshout_enabled&(!debuff.ap_reduction.up|(debuff.demoralizing_shout.up&debuff.demoralizing_shout.remains<3))
# 雷霆一击
actions.single+=/thunder_clap,if=!debuff.attack_speed_reduction.up|(debuff.thunder_clap.up&debuff.attack_speed_reduction.remains<3)
# 震荡波
actions.single+=/shockwave
# 毁灭打击
actions.single+=/devastate,if=cooldown.shield_slam.remains&!buff.revenge_usable.up&(action.shield_slam.spend<=rage.current-action.devastate.spend+rage_gain*gcd)

## Cleave
actions.aoe+=/shattering_throw,if=boss&buff.bloodlust.up&debuff.shattering_throw.down
actions.aoe+=/defensive_stance,use_off_gcd=1,if=buff.defensive_stance.down
# 顺劈斩
actions.aoe+=/cleave,use_off_gcd=1,if=rage.current>=settings.queueing_threshold&rage.current>action.cleave.spend+((cooldown.shield_slam.remains<2)*action.shield_slam.spend)
actions.aoe+=/thunder_clap
actions.aoe+=/shockwave,if=active_enemies>3
actions.aoe+=/revenge
actions.aoe+=/shockwave
actions.aoe+=/shield_block,use_off_gcd=1,if=(aggro|debuff.training_dummy.up)&cooldown.thunder_clap.remains&!buff.revenge_usable.up&cooldown.thunder_clap.remains&cooldown.shield_slam.up&rage>=action.shield_slam.spend
actions.aoe+=/shield_slam
actions.aoe+=/commanding_shout,if=assigned_shout.commanding_shout&buff.shout.remains<=4&buff.commanding_shout.remains<=4
actions.aoe+=/battle_shout,if=assigned_shout.battle_shout&buff.shout.remains<=4&buff.battle_shout.remains<=4
actions.aoe+=/demoralizing_shout,if=settings.debuff_demoshout_enabled&(!debuff.major_ap_reduction.up|(debuff.demoralizing_shout.up&debuff.demoralizing_shout.remains<3))
actions.aoe+=/devastate,if=cooldown.shield_slam.remains&!buff.revenge_usable.up&(action.shield_slam.spend<=rage.current-action.devastate.spend+rage_gain*gcd)